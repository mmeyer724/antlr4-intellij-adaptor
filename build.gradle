buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.intellij' version '1.17.3'
    id 'antlr'
}

wrapper {
    gradleVersion = '8.0.2'
}

group 'org.antlr'
version libraryVersion

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

intellij {
    version.set(ideaVersion)

    pluginName.set('antlr4-intellij-adaptor')
    downloadSources.set(true)
    updateSinceUntilBuild.set(false)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.antlr:antlr4-runtime:$antlr4Version") {
        exclude group: 'com.ibm.icu', module: 'icu4j'
    }
    antlr "org.antlr:antlr4:$antlr4Version"

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.opentest4j:opentest4j:1.3.0")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
    dependsOn(':generateGrammarSource')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
